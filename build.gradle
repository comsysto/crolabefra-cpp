buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.3"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.gradle.plugin-publish"

group = 'com.comsysto.gradle'
version = new Version('0.3.0-SNAPSHOT')
status = version.status

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'de.undercouch:gradle-download-task:2.1.0'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

pluginBundle {
    website = 'https://github.com/comsysto/crolabefra-cpp'
    vcsUrl = 'https://github.com/comsysto/crolabefra-cpp'
    description = 'CroLaBeFra Gradle Plugin for C/C++ with Hayai'
    tags = ['Hayai', 'gradle', 'crolabefra', 'native', 'microbenchmarks', 'C++']

    plugins {
        crolabefraCppPlugin {
            id = 'com.comsysto.gradle.crolabefra.cpp'
            displayName = 'CroLaBeFra Gradle Plugin for C/C++ with Hayai'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'crolabefra-cpp-plugin'
            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            pom.withXml {
                def Node pomRoot = asNode()
                pomRoot.appendNode('name', 'CroLaBeFra Gradle Plugin for C/C++ with Hayai')
                pomRoot.appendNode('url', 'https://github.com/comsysto/crolabefra-cpp')

                def issues = pomRoot.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', 'https://github.com/comsysto/crolabefra-cpp/issues')

                def scm = pomRoot.appendNode('scm')
                scm.appendNode('url', 'https://github.com/comsysto/crolabefra-cpp')
                scm.appendNode('connection', 'scm:git:https://github.com/comsysto/crolabefra-cpp.git')
                scm.appendNode('developerConnection', 'scm:git:git@github.com:comsysto/crolabefra-cpp.git')

                def license = pomRoot.appendNode('licenses').appendNode('license');
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = pomRoot.appendNode('developers').appendNode('developer');
                developer.appendNode('id', 'bensteinert')
                developer.appendNode('name', 'Benjamin Steinert')
                developer.appendNode('organization', 'comSysto')
                developer.appendNode('organizationUrl', 'http://comsysto.com')
            }
        }
    }
}

bintray {
    user = System.getProperty('bintrayUsername')
    key = System.getProperty('bintrayKey')
    //dryRun = true
    publish = false

    publications = ['mavenJava']
    pkg {
        userOrg = 'comsysto'
        repo = 'crolabefra'
        name = 'crolabefra-cpp-plugin'
        desc = 'CroLaBeFra Gradle Plugin for C/C++ with Hayai'
        licenses = ['Apache-2.0']
        labels = ['Hayai', 'gradle', 'crolabefra', 'native']
        websiteUrl = 'https://github.com/comsysto/crolabefra-cpp'
        issueTrackerUrl = 'https://github.com/comsysto/crolabefra-cpp/issues'
        vcsUrl = 'https://github.com/comsysto/crolabefra-cpp'
        version {
            vcsTag = "${project.version}"
            released  = new Date()
            desc = 'Library maintenance release'
            attributes = ['gradle-plugin': 'com.comsysto.gradle.crolabefra.cpp:com.comsysto.gradle:crolabefra-cpp-plugin']
        }
    }
}


// Taken from https://github.com/bintray/bintray-examples/blob/master/gradle-multi-example/build.gradle

import java.text.SimpleDateFormat
class Version {
    String originalVersion
    String thisVersion
    String status
    Date buildTime

    Version(String versionValue) {
        buildTime = new Date()
        originalVersion = versionValue
        if (originalVersion.endsWith('-SNAPSHOT')) {
            status = 'integration'
            thisVersion = originalVersion.substring(0, originalVersion.length() - 'SNAPSHOT'.length()) + getTimestamp()
        } else {
            status = 'release'
            thisVersion = versionValue
        }
    }

    String getTimestamp() {
        // Convert local file timestamp to UTC
        def format = new SimpleDateFormat('yyyyMMddHHmmss')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')));
        return format.format(buildTime)
    }

    String toString() {
        thisVersion
    }
}